/*
 * ==== Build Commands ====
 *
 * Update version:
 * > ./gradlew setVersion -PnewVersion=0.0.1-SNAPSHOT
 *
 * Build OAG executable and library:
 * > ./gradlew clean assemble
 *
 * Output: - build/libs/oag.jar (Executable JAR)
 *         - build/libs/oag-<VERSION>-plain.jar (OAG lib only, no dependencies)
 *
 * ==== Deploy to Maven Central ====
 * Build OAG library and deploy to maven central:
 * > ./gradlew assemble publishAllPublicationsToProjectLocalRepository zipMavenCentralPortalPublication releaseMavenCentralPortalPublication -PgpgSign=true -x test
 *   or
 * > ./gradlew assemble publishAllPublicationsToProjectLocalRepository zipMavenCentralPortalPublication releaseMavenCentralPortalPublication -PgpgSign=true -PskipTests
 *
 * Environment Variables must be set:
 * - SIGNING_PASSWORD,SIGNING_KEY (containing the password for the signing key, and the encrypted signing key used to sign the code)
 * - MAVEN_CENTRAL_PORTAL_USERNAME, MAVEN_CENTRAL_PORTAL_PASSWORD (containing the Maven/Sonatype/Nexus credentials).
 */

plugins {
    id 'java'
    id 'org.springframework.boot' version '4.0.1'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.danilopianini.publish-on-central' version '9.1.9'
    id 'maven-publish'
    id 'signing'
}

group = 'org.owasp'
version = '0.7.2-SNAPSHOT'
java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
    maven { url = uri('https://repo.spring.io/release') }
}

ext {
    springCloudVersion = '2025.1.0'
}

dependencies {
    // Security fixes section

    // Main dependencies
    implementation 'com.google.guava:guava:33.5.0-jre'
    implementation 'org.yaml:snakeyaml:2.5'
    implementation 'com.nimbusds:oauth2-oidc-sdk:11.30.1'
    implementation 'com.nimbusds:nimbus-jose-jwt:10.6'
    implementation 'io.github.artsok:rerunner-jupiter:2.1.6'
    implementation 'org.mapdb:mapdb:3.1.0'
    implementation 'org.apache.commons:commons-lang3:3.20.0'
    implementation 'commons-codec:commons-codec:1.20.0'
    implementation 'org.antlr:ST4:4.3.4'
    implementation 'com.github.ben-manes.caffeine:caffeine:3.2.3'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml'

    // Spring Cloud dependencies
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway-server-webflux'
    implementation 'org.springframework.cloud:spring-cloud-starter-loadbalancer'

    // Test dependencies
    testImplementation 'org.springframework.boot:spring-boot-starter-webflux-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine'
    testImplementation 'org.springframework.cloud:spring-cloud-contract-wiremock'
    testImplementation 'org.wiremock.integrations:wiremock-spring-boot:4.0.8'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
    // Allow skipping tests via command line property
    enabled = !project.hasProperty('skipTests')
}

tasks.named('bootJar') {
    archiveFileName = 'oag.jar'
    archiveClassifier = 'exec'
}

tasks.named('jar') {
    enabled = true
}

// Task to set the project version (equivalent to Maven's versions:set)
tasks.register('setVersion') {
    doLast {
        if (project.hasProperty('newVersion')) {
            // Update version in build.gradle
            def buildFile = file('build.gradle')
            def content = buildFile.text
            def updatedContent = content.replaceAll("version\\s*=\\s*['\"].*?['\"]", "version = '${project.newVersion}'")
            buildFile.text = updatedContent
            println "Version updated to ${project.newVersion}"
        } else {
            logger.error("Please provide a newVersion property: -PnewVersion=X.Y.Z")
            throw new IllegalArgumentException("Missing required property: newVersion")
        }
    }
}

tasks.register('generateCertificate', Exec) {
    workingDir '.'
    commandLine 'keytool', '-genkeypair', '-alias', 'tls', '-keyalg', 'RSA', 
                '-keystore', 'keystore.pkcs12', '-storetype', 'PKCS12',
                '-keysize', '2048', '-keypass', 'password', '-storepass', 'password',
                '-validity', '720', '-dname', 'cn=demo, ou=oag, o=owasp, c=com'
}

tasks.register('deleteKeystore') {
    doLast {
        delete 'keystore.pkcs12'
    }
}

tasks.named('clean') {
    dependsOn tasks.named('deleteKeystore')
}

// Make deleteKeystore run first in the build process
tasks.named('compileJava') {
    dependsOn tasks.named('deleteKeystore')
    // Make generateCertificate run after compile but before tests
    finalizedBy tasks.named('generateCertificate')
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishOnCentral {
    repoOwner.set("The-OAG-Development-Project")
    projectDescription.set("An elephant strong web application gateway that handles oidc/oauth2 authentication and session management.")
    projectLongName.set("OWASP Application Gateway")
    licenseName.set("Apache License, Version 2.0")
    licenseUrl.set("http://www.apache.org/licenses/LICENSE-2.0")
    projectUrl.set("https://github.com/The-OAG-Development-Project/Application-Gateway")
    scmConnection.set("scm:git:https://github.com/The-OAG-Development-Project/Application-Gateway")
}

publishing {
    publications {
        OSSRH(MavenPublication) {
            // The Plugin org.danilopianini.publish-on-central automatically creates a repository for maven central portal
            // and uses credentials defined in MAVEN_CENTRAL_PORTAL_USERNAME, MAVEN_CENTRAL_PORTAL_PASSWORD env variables.
            // the publication it uses is called OSSRH. We extend it here.
            // group and version is taken from variables above
            artifactId = 'oag'
            // from components.java is done by plugin publish-on-central

            pom {
                developers {
                    developer {
                        id = 'gianlucafrei'
                        name = 'Gian-Luca Frei'
                        email = 'gian-luca.frei@owasp.org'
                    }
                    developer {
                        id = 'Padi-owasp'
                        name = 'Patrick Steger'
                        email = 'padi.steger@owasp.org'
                    }
                }
            }
        }
    }
}

signing {
    required = { project.hasProperty('gpgSign') && project.gpgSign.toBoolean() }
    def signingKey = System.env.SIGNING_KEY
    def signingPassword = System.env.SIGNING_PASSWORD
    useInMemoryPgpKeys(signingKey, signingPassword)
    // sign publishing.publications.OSSRH is called by plugin publish-on-central
}